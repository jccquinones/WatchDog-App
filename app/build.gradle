apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.1"
    defaultConfig {
        applicationId "cl.ucn.disc.dam.watchdogapp"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    buildTypes {
        release {
            postprocessing {
                crunchPngs true
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                zipAlignEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            versionNameSuffix "-release"
        }
        debug {
            postprocessing {
                crunchPngs false
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                zipAlignEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
        // disale 'InlinedApi', 'AllowBackup'
        showAll true
        htmlReport true
    }
}

// File output name
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "dnews-${variant.versionCode}-${variant.name}.apk"
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Simple Logging Facade 4 Java
    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'com.android.support:appcompat-v7:26.1.0'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    testAnnotationProcessor 'org.projectlombok:lombok:1.16.18'
    testImplementation 'org.projectlombok:lombok:1.16.18'

    testImplementation 'org.assertj:assertj-core:3.8.0'

    // Commons Lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    // Lombok Android
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.18'

    // DBFLow
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.1.2'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-core:4.1.2'
    implementation 'com.github.Raizlabs.DBFlow:dbflow:4.1.2'

    implementation 'com.android.support:recyclerview-v7:26.1.0'
}
